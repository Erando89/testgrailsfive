buildscript {
    repositories {
        mavenLocal()

        maven { url "https://plugins.gradle.org/m2/" }
        maven { url "https://repo.grails.org/grails/core" }
    }
    dependencies {
        classpath "org.grails:grails-gradle-plugin:$grailsGradlePluginVersion"
        classpath "org.grails.plugins:hibernate5:7.0.7"
        classpath "gradle.plugin.com.github.erdi.webdriver-binaries:webdriver-binaries-gradle-plugin:2.1"
        classpath 'org.asciidoctor:asciidoctor-gradle-jvm:3.3.2'
    }
}

plugins {
    id "groovy"
    id "com.gorylenko.gradle-git-properties" version "2.4.1"
    // nachfolgendes Plugin wird nur unter Windows und nur für run-app gebraucht, sonst nicht
    id "ua.eshepelyuk.ManifestClasspath" version "1.0.0" apply false
    id "idea" // einige spezielle Dinge für IntelliJ
}

gitProperties {
    gitPropertiesResourceDir = new File("${project.rootDir}/src/main/resources/public") // neu
    gitPropertiesDir = new File("${project.rootDir}/src/main/resources/public") // alt
    dateFormat = "dd.MM.yyyy' 'HH:mm"
    dateFormatTimeZone = "CET"
}

version "0.1"
group "testgrailsfive"

apply plugin:"eclipse"
apply plugin:"idea"
apply plugin:"war"
apply plugin:"org.grails.grails-web"
apply plugin:"com.github.erdi.webdriver-binaries"
apply plugin:"org.grails.grails-gsp"

repositories {
    mavenLocal()

    maven { url "https://repo.spring.io/milestone" }
    maven { url "https://repo.grails.org/grails/core" }
    maven { url "https://repo.grails.org/grails/plugins" }
}

configurations {
    developmentOnly
    runtimeClasspath {
        extendsFrom developmentOnly
    }
}

dependencies {
    configurations.all {
        exclude group: "log4j", module: "log4j"
        exclude group: 'commons-logging', module: 'commons-logging'
        exclude group: 'org.slf4j', module: 'jcl-over-slf4j'
        exclude group: 'aopalliance', module: 'aopalliance'
        exclude group: 'net.sf.ehcache', module: 'ehcache'
        exclude group: 'net.sf.ehcache', module: 'ehcache-core'
        exclude group: 'org.apache.geronimo.specs', module: 'geronimo-jta_1.1_spec'
//        exclude group: 'commons-beanutils', module: 'commons-beanutils-core'
        exclude group: 'com.google.collections', module:'google-collections'
        exclude group: 'org.hamcrest', module: 'hamcrest-core'
// //       exclude group: 'com.sun.istack', module: 'istack-commons-runtime'
        exclude group: 'javax.mail', module: 'javax.mail-api'
//        exclude group: 'javax.xml.bind', module: 'jsr173_api'
        exclude group: 'com.sun.xml.bind', module: 'jaxb-impl'
        exclude group: 'javax.transaction', module: 'jta'
        exclude group: 'javax.el', module: 'javax.el-api'
    }

    implementation group: 'com.sun.activation', name: 'jakarta.activation', version: '1.2.2'

    implementation "org.springframework.boot:spring-boot-starter-logging"
    implementation "org.springframework.boot:spring-boot-autoconfigure"

    implementation "org.grails:grails-core"

    implementation "org.springframework.boot:spring-boot-starter-actuator"
            implementation "org.springframework.boot:spring-boot-starter-tomcat"

    // Beim Upgrade auf Grails 5 gab es hier ein Problem, dass das Modul nicht erkannt wurde, obwohl es über
    // grails-dependencies reinkam. Daher oben exkludiert und gesondert eingebunden
    implementation ("org.grails:grails-dependencies") { //+
        exclude group: "org.grails", module: "grails-codecs"
    }
    implementation "org.grails:grails-codecs"

    implementation "org.grails:grails-web-boot"

    implementation "org.grails:grails-logging"
    implementation ("org.grails:grails-logging") {
        exclude group: 'ch.qos.logback', module: 'logback-classic'
        exclude group: 'ch.qos.logback', module: 'logback-core'
    }
    implementation "ch.qos.logback:logback-classic:1.2.7"
    implementation "ch.qos.logback:logback-core:1.2.7"

    implementation "org.grails:grails-plugin-rest"
    implementation "org.grails:grails-plugin-databinding"
    implementation "org.grails:grails-plugin-i18n"
    implementation "org.grails:grails-plugin-services"
    implementation "org.grails:grails-plugin-url-mappings"
    implementation "org.grails:grails-plugin-interceptors"
    implementation "org.grails.plugins:gsp"
    implementation 'org.grails.plugins:cache:5.0.1'
    implementation "org.grails.plugins:async"
    implementation "org.grails.plugins:scaffolding:4.1.0"
    implementation "org.grails.plugins:events"

    // TBSEOWNT-18332 [Plugins] org.grails.plugins:hibernate5 updaten
    implementation("org.grails.plugins:hibernate5:7.0.7") // Achtung: Gleich halten mit der Version im Abschnitt "buildscript" !
    // TBSEOWNT-18330 [Plugins] org.hibernate:hibernate-* updaten
    implementation("org.hibernate:hibernate-core:5.4.33.Final") // Letzte 5.4.x Version!
    implementation("org.hibernate:hibernate-ehcache:5.4.33.Final") // Letzte 5.4.x Version!

    implementation 'org.apache.commons:commons-lang3:3.12.0'
    implementation 'org.apache.commons:commons-text:1.10.0'
    implementation 'com.fasterxml.jackson.core:jackson-core:2.14.0'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.14.0'
    implementation 'com.fasterxml.jackson.core:jackson-annotations:2.14.0'
    // needed for MappingJackson2XmlView
    implementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-xml:2.14.0'

    implementation group: 'com.univocity', name: 'univocity-parsers', version: '2.9.1'
    implementation group: 'net.sourceforge.nekohtml', name: 'nekohtml', version: '1.9.22'

    implementation 'org.jdom:jdom2:2.0.6.1'
    // neue Spring-Mobile - ist ein Fork, wo jetzt auch Crawler- und Voice-Erkennung eingebaut ist

    // Version in Grails 3 vorerst nicht ändern
    implementation 'com.h2database:h2:2.1.214'
    // Latenz- und Fehlertoleranz-Bibliothek ... Circuit Breaker und so
    implementation 'com.netflix.hystrix:hystrix-core:1.5.18'
    // rewrite-komponente
    implementation('info.bliki.wiki:bliki-core:3.1.0') {
        exclude group: 'org.apache.commons', module: 'commons-lang3'
        exclude group: 'ch.qos.logback', module: 'logback-classic' // kommt mit grails-logging mit
    }
    // fuer JavaMelody
    // TBSEOWNT-5179 : https://code.google.com/p/javamelody/wiki/UserGuide#Dependencies
    implementation 'com.thoughtworks.xstream:xstream:1.4.20'
    implementation 'cglib:cglib-nodep:3.3.0'
    implementation "net.sourceforge.cardme:cardme:0.4.0"
    implementation "org.grails.plugins:cache-ehcache:3.0.0"
    implementation 'org.grails.plugins:cache-headers:2.0.2'
    implementation 'javax.cache:cache-api:1.1.1'
    // Jira-7394 + 8009
    implementation 'org.grails.plugins:browser-detection:3.4.0'
    implementation "org.grails.plugins:quartz:2.0.13"
    implementation group: 'org.quartz-scheduler', name: 'quartz', version: '2.3.2'
    implementation("org.grails.plugins:mail:3.0.0") {
        exclude group: 'javax.activation', module: 'activation' // wegen doppelter Klassen
    }

    implementation("org.grails.plugins:grails-melody-plugin:1.80.0") {
        exclude group: 'net.bull.javamelody', module: 'javamelody-core'
    }
    implementation 'net.bull.javamelody:javamelody-core:1.91.0'

    // Ersatz für die veraltete Version in grails-xss-sanitizer
    implementation('org.owasp.esapi:esapi:2.5.1.0') {
        exclude group: 'xalan', module: 'xalan'
        exclude group: 'xom', module: 'xom'
        exclude group: 'commons-beanutils', module: 'commons-beanutils-core'
    }
    implementation 'org.owasp.antisamy:antisamy:1.7.1'

    implementation 'org.grails.plugins:spring-security-core:4.0.5'

    // TBSEOWNT-18017 [Inkrement-2] Token-Abfrage & Validierung ohne Grails Plugin selbst erledigen
    // ! DON'T USE THIS ANYMORE: compile 'org.grails.plugins:spring-security-oauth2-provider:4.0.0-RC1'

    implementation 'org.springframework.security.oauth:spring-security-oauth2:2.5.2.RELEASE'

    implementation('org.grails.plugins:converters') {  // Provides JSON and XML converters
        exclude group: 'org.apache.commons', module: 'commons-lang3'
    }
    implementation 'domurtag.plugins:grails-simple-captcha:1.0.0-grails3'




    implementation group: 'com.google.guava', name: 'guava', version: '31.1-jre'
    implementation group: 'javax.inject', name: 'javax.inject', version: '1'

    implementation 'org.influxdb:influxdb-java:2.23'
    profile "org.grails.profiles:web"

    runtimeOnly "com.h2database:h2"

    runtimeOnly "org.apache.httpcomponents:httpcore:4.4.15"
    runtimeOnly "org.apache.httpcomponents:httpclient:4.5.13"

    // TBSEOWNT-18327 [Plugins] mysql:mysql-connector-java upgraden
    runtimeOnly  'com.mysql:mysql-connector-j:8.0.32'

    testImplementation 'com.stehno.ersatz:ersatz:2.0.0'
    testRuntimeOnly 'org.gmetrics:GMetrics:2.1.0'
    testRuntimeOnly 'net.sourceforge.htmlunit:htmlunit:2.35.0'
    testImplementation group: 'org.xmlunit', name: 'xmlunit-core', version: '2.9.0'
    testImplementation group: 'org.xmlunit', name: 'xmlunit-matchers', version: '2.9.0'
    testImplementation "org.grails:grails-test-mixins:3.3.0"
    testImplementation "org.grails:grails-gorm-testing-support"

    // nachfolgendes für Geb-test (laufen nicht mit 'testImplementation')
    testImplementation "org.grails:grails-web-testing-support"
    testImplementation 'org.hamcrest:hamcrest-all:1.3'
    testImplementation 'org.grails.plugins:geb'
    testImplementation 'org.seleniumhq.selenium:htmlunit-driver:2.52.0'
    testImplementation "org.seleniumhq.selenium:selenium-api:$seleniumVersion"
    testImplementation "org.seleniumhq.selenium:selenium-remote-driver:$seleniumVersion"
    testImplementation "org.seleniumhq.selenium:selenium-support:$seleniumVersion"
    testImplementation "org.seleniumhq.selenium:selenium-firefox-driver:$seleniumVersion"
    testImplementation "org.seleniumhq.selenium:selenium-chrome-driver:$seleniumVersion"
    // Ende Geb-Test

    implementation group: 'org.slf4j', name: 'slf4j-api', version: "$slf4jVersion"
    implementation group: 'org.slf4j', name: 'jul-to-slf4j', version: "$slf4jVersion"
    implementation group: 'org.slf4j', name: 'log4j-over-slf4j', version: "$slf4jVersion"

    implementation group: 'eu.bitwalker', name: 'UserAgentUtils', version: '1.21'
    implementation group: 'org.codehaus.groovy', name: 'groovy-dateutil', version: '3.0.13'

    implementation group: 'org.apache.commons', name: 'commons-pool2', version: '2.11.1'

    // (Optional) Native OSX file watcher
    runtimeOnly "io.methvin:directory-watcher:0.17.0"

    // TBSEOWNT-14692 -> CMP-String testen
    implementation group: 'com.iabtcf', name: 'iabtcf-decoder', version: '2.0.10'


}

bootRun {
    ignoreExitValue true
    jvmArgs(
        '-Dspring.output.ansi.enabled=always', 
        '-noverify', 
        '-XX:TieredStopAtLevel=1',
        '-Xmx1024m')
    sourceResources sourceSets.main
    String springProfilesActive = 'spring.profiles.active'
    systemProperty springProfilesActive, System.getProperty(springProfilesActive)
}

tasks.withType(GroovyCompile) {
    configure(groovyOptions) {
        forkOptions.jvmArgs = ['-Xmx1024m']
    }
}

tasks.withType(Test) {
    useJUnitPlatform()
}

webdriverBinaries {
    chromedriver chromeDriverVersion
    geckodriver geckoDriverVersion
}

tasks.withType(Test) {
    systemProperty "geb.env", System.getProperty('geb.env')
    systemProperty "geb.build.reportsDir", reporting.file("geb/integrationTest")
    if (!System.getenv().containsKey('GITHUB_ACTIONS')) {
        systemProperty 'webdriver.chrome.driver', System.getProperty('webdriver.chrome.driver')
        systemProperty 'webdriver.gecko.driver', System.getProperty('webdriver.gecko.driver')
    } else {
        systemProperty 'webdriver.chrome.driver', "${System.getenv('CHROMEWEBDRIVER')}/chromedriver"
        systemProperty 'webdriver.gecko.driver', "${System.getenv('GECKOWEBDRIVER')}/geckodriver"
    }
}
